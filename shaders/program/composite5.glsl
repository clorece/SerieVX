/////////////////////////////////////
// Complementary Shaders by EminGT //
/////////////////////////////////////

//Common//
#include "/lib/common.glsl"

#define OVERWORLD_LUT                8          //[0 1 2 3 4 5 6 7 8 9]
#define NETHER_LUT                2          //[0 1 2 3 4 5 6 7 8 9]
#define END_LUT                 1          //[0 1 2 3 4 5 6 7 8 9]

#define OVERWORLD_LUT_I            0.3          //[0.0 0.1 0.2 0.3 0.4 0.5 0.6 0.7 0.8 0.9 1.0]
#define NETHER_LUT_I               1.0          //[0.0 0.1 0.2 0.3 0.4 0.5 0.6 0.7 0.8 0.9 1.0]
#define END_LUT_I                  1.0          //[0.0 0.1 0.2 0.3 0.4 0.5 0.6 0.7 0.8 0.9 1.0]

#ifdef OVERWORLD
    #define SELECTED_LUT OVERWORLD_LUT
    #define SELECTED_LUT_I OVERWORLD_LUT_I
#endif
#ifdef NETHER
    #define SELECTED_LUT NETHER_LUT
    #define SELECTED_LUT_I NETHER_LUT_I
#endif
#ifdef END
    #define SELECTED_LUT END_LUT
    #define SELECTED_LUT_I END_LUT_I
#endif

#define GBPreset 18 // [1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32]

const float eyeBrightnessHalflife = 1.0f;

//////////Fragment Shader//////////Fragment Shader//////////Fragment Shader//////////
#ifdef FRAGMENT_SHADER

noperspective in vec2 texCoord;

#if defined BLOOM_FOG || LENSFLARE_MODE > 0 && defined OVERWORLD
    flat in vec3 upVec, sunVec;
#endif

//Pipeline Constants//

//Common Variables//
float pw = 1.0 / viewWidth;
float ph = 1.0 / viewHeight;

vec2 view = vec2(viewWidth, viewHeight);

#if defined BLOOM_FOG || LENSFLARE_MODE > 0 && defined OVERWORLD
    float SdotU = dot(sunVec, upVec);
    float sunFactor = SdotU < 0.0 ? clamp(SdotU + 0.375, 0.0, 0.75) / 0.75 : clamp(SdotU + 0.03125, 0.0, 0.0625) / 0.0625;
#endif

//Common Functions//
void DoBSLTonemap(inout vec3 color) {
    color = T_EXPOSURE * color;
    color = color / pow(pow(color, vec3(TM_WHITE_CURVE)) + 1.0, vec3(1.0 / TM_WHITE_CURVE));
    color = pow(color, mix(vec3(T_LOWER_CURVE), vec3(T_UPPER_CURVE), sqrt(color)));

    color = pow(color, vec3(1.0 / 2.2));
}


vec3 reinhard_jodie(vec3 v)
{
    float l = luminance(v);
    vec3 tv = v / (1.0f + v);
    return lerp(v / (1.0f + l), tv, tv);
}

void DoBSLColorSaturation(inout vec3 color) {
    float grayVibrance = (color.r + color.g + color.b) / 3.0;
    float graySaturation = grayVibrance;
    if (T_SATURATION < 1.00) graySaturation = dot(color, vec3(0.299, 0.587, 0.114));

    float mn = min(color.r, min(color.g, color.b));
    float mx = max(color.r, max(color.g, color.b));
    float sat = (1.0 - (mx - mn)) * (1.0 - mx) * grayVibrance * 5.0;
    vec3 lightness = vec3((mn + mx) * 0.5);

    color = mix(color, mix(color, lightness, 1.0 - T_VIBRANCE), sat);
    color = mix(color, lightness, (1.0 - lightness) * (2.0 - T_VIBRANCE) / 2.0 * abs(T_VIBRANCE - 1.0));
    color = color * T_SATURATION - graySaturation * (T_SATURATION - 1.0);
}


vec3 Tonemap_ACES(vec3 color) {
    color *= TONEMAP_EXPOSURE * (10.0 / TONEMAP_WHITE_POINT); // Scale factor to match original 0.35 at default 2.0

    const mat3 m1 = mat3(
        0.59719, 0.07600, 0.02840,
        0.35458, 0.90834, 0.13383,
        0.04823, 0.01566, 0.83777
    );
    const mat3 m2 = mat3(
        1.60475, -0.10208, -0.00327,
        -0.53108,  1.10813, -0.07276,
        -0.07367, -0.00605,  1.07602
    );

    vec3 v = m1 * color;
    vec3 a = v * (v + 0.0245786) - 0.000090537;
    vec3 b = v * (0.983729 * v + 0.4329510) + 0.238081;
    vec3 tonemapped = m2 * (a / b);

    // Apply unified saturation
    float lum = dot(tonemapped, vec3(0.2126, 0.7152, 0.0722));
    tonemapped = mix(vec3(lum), tonemapped, TONEMAP_SATURATION);

    // Apply unified contrast
    tonemapped = mix(vec3(0.5), tonemapped, TONEMAP_CONTRAST * 0.999);

    // Apply unified gamma and black point
    vec3 result = pow(clamp(tonemapped, 0.0, 1.0), vec3(1.0 / TONEMAP_GAMMA));
    return mix(vec3(TONEMAP_BLACK_POINT), vec3(1.0), result);
}

vec3 Uchimura(vec3 x, float P, float a, float m, float l, float c, float b) {
    // Uchimura 2017, "HDR theory and practice"
    // Math: https://www.desmos.com/calculator/gslcdxvipg
    // Source: https://www.slideshare.net/nikuque/hdr-theory-and-practicce-jp
    float l0 = ((P - m) * l) / a;
    float L0 = m - m / a;
    float L1 = m + (1.0 - m) / a;
    float S0 = m + l0;
    float S1 = m + a * l0;
    float C2 = (a * P) / (P - S1);
    float CP = -C2 / P;

    vec3 w0 = vec3(1.0) - smoothstep(vec3(0.0), vec3(m), x);
    vec3 w2 = step(vec3(m + l0), x);
    vec3 w1 = vec3(1.0) - w0 - w2;

    vec3 T = m * pow(x / m, vec3(c)) + b;
    vec3 S = P - (P - S1) * exp(CP * (x - S0));
    vec3 L = m + a * (x - m);

    return T * w0 + L * w1 + S * w2;
}

vec3 Tonemap_Uchimura(vec3 color) {
    const float P = 1.0;  // max display brightness
    const float a = 0.8;  // contrast
    const float m = 0.22; // linear section start
    const float l = 0.4;  // linear section length
    const float c = 1.22; // black
    const float b = 0.0;  // pedestal

    // Apply unified exposure
    color *= TONEMAP_EXPOSURE;

    vec3 tonemapped = Uchimura(color, P, a, m, l, c, b);

    // Apply unified saturation
    float lum = dot(tonemapped, vec3(0.2126, 0.7152, 0.0722));
    tonemapped = mix(vec3(lum), tonemapped, TONEMAP_SATURATION);

    // Apply unified contrast
    tonemapped = mix(vec3(0.5), tonemapped, TONEMAP_CONTRAST);

    // Apply unified gamma and black point
    vec3 result = pow(clamp(tonemapped, 0.0, 1.0), vec3(1.0 / TONEMAP_GAMMA));
    return mix(vec3(TONEMAP_BLACK_POINT), vec3(1.0), result);
}

vec3 Tonemap_Lottes(vec3 x) {
    // Lottes 2016, "Advanced Techniques and Optimization of HDR Color Pipelines"
    const float a = 1.0;        // Contrast curve power
    const float d = 0.977;      // Toe adjustment
    const float hdrMax = 8.0;   // Maximum HDR input value
    const float midIn = 0.18;   // Input middle grey
    const float midOut = 0.267; // Output middle grey

    // Apply unified exposure
    x *= TONEMAP_EXPOSURE * 0.35; // Scale factor to match original 0.7 at default 2.0

    // Precomputed curve parameters
    const float b =
        (-pow(midIn, a) + pow(hdrMax, a) * midOut) /
        ((pow(hdrMax, a * d) - pow(midIn, a * d)) * midOut);
    const float c =
        (pow(hdrMax, a * d) * pow(midIn, a) - pow(hdrMax, a) * pow(midIn, a * d) * midOut) /
        ((pow(hdrMax, a * d) - pow(midIn, a * d)) * midOut);

    vec3 tonemapped = pow(x, vec3(a)) / (pow(x, vec3(a * d)) * b + c);

    // Apply unified saturation
    float lum = dot(tonemapped, vec3(0.2126, 0.7152, 0.0722));
    tonemapped = mix(vec3(lum), tonemapped, TONEMAP_SATURATION);

    // Apply unified contrast
    tonemapped = mix(vec3(0.5), tonemapped, TONEMAP_CONTRAST);

    // Apply unified gamma and black point
    vec3 result = pow(clamp(tonemapped, 0.0, 1.0), vec3(1.0 / TONEMAP_GAMMA));
    return mix(vec3(TONEMAP_BLACK_POINT), vec3(1.0), result);
}

vec3 Hable_Partial(vec3 x) {
    const float A = 0.27;  // Shoulder Strength
    const float B = 0.50;  // Linear Strength
    const float C = 0.12;  // Linear Angle
    const float D = 0.22;  // Toe Strength
    const float E = 0.02;  // Toe Numerator
    const float F = 0.30;  // Toe Denominator
    
    return ((x * (A * x + C * B) + D * E) / (x * (A * x + B) + D * F)) - E / F;
}

vec3 Tonemap_Hable(vec3 color) {
    // Apply unified exposure
    vec3 curr = Hable_Partial(color * TONEMAP_EXPOSURE * 2.0);
    vec3 whiteScale = vec3(1.0) / Hable_Partial(vec3(TONEMAP_WHITE_POINT));
    vec3 tonemapped = curr * whiteScale;

    // Apply unified saturation
    float lum = dot(tonemapped, vec3(0.2126, 0.7152, 0.0722));
    tonemapped = mix(vec3(lum), tonemapped, TONEMAP_SATURATION);

    // Apply unified contrast
    tonemapped = mix(vec3(0.5), tonemapped, TONEMAP_CONTRAST * 1.0);
    
    // Apply unified gamma and black point
    vec3 result = pow(clamp(tonemapped, 0.0, 1.0), vec3(1.0 / TONEMAP_GAMMA));
    return mix(vec3(TONEMAP_BLACK_POINT), vec3(1.0), result);
}

// Reinhard-Jodie tonemapper
vec3 Tonemap_Reinhard(vec3 color) {
    // Apply unified exposure
    color *= TONEMAP_EXPOSURE;

    float lum = dot(color, vec3(0.2126, 0.7152, 0.0722));
    vec3 tv = color / (1.0 + color);
    vec3 tonemapped = mix(color / (1.0 + lum), tv, tv);

    // Apply unified saturation
    float lum2 = dot(tonemapped, vec3(0.2126, 0.7152, 0.0722));
    tonemapped = mix(vec3(lum2), tonemapped, TONEMAP_SATURATION);

    // Apply unified contrast
    tonemapped = mix(vec3(0.5), tonemapped, TONEMAP_CONTRAST);

    // Apply unified gamma and black point
    vec3 result = pow(clamp(tonemapped, 0.0, 1.0), vec3(1.0 / TONEMAP_GAMMA));
    return mix(vec3(TONEMAP_BLACK_POINT), vec3(1.0), result);
}

// AGX Tonemapper - maintains hue in bright areas better than ACES
vec3 AGX_DefaultContrastApprox(vec3 x) {
    vec3 x2 = x * x;
    vec3 x4 = x2 * x2;
    return + 15.5 * x4 * x2
           - 40.14 * x4 * x
           + 31.96 * x4
           - 6.868 * x2 * x
           + 0.4298 * x2
           + 0.1191 * x
           - 0.00232;
}

vec3 Tonemap_AGX(vec3 color) {
    // Apply exposure
    color *= TONEMAP_EXPOSURE;
    
    // AGX input transform (sRGB to AGX log space)
    const mat3 agx_mat = mat3(
        0.842479062253094, 0.0423282422610123, 0.0423756549057051,
        0.0784335999999992, 0.878468636469772, 0.0784336,
        0.0792237451477643, 0.0791661274605434, 0.879142973793104
    );
    
    color = agx_mat * color;
    
    // Log2 space encoding
    color = max(color, 1e-10);
    color = log2(color);
    color = (color - (-10.0)) / (6.5 - (-10.0)); // min/max exposure
    color = clamp(color, 0.0, 1.0);
    
    // Apply sigmoid contrast
    color = AGX_DefaultContrastApprox(color);
    
    // AGX output transform (AGX to sRGB)
    const mat3 agx_mat_inv = mat3(
        1.19687900512017, -0.0528968517574562, -0.0529716355144438,
        -0.0980208811401368, 1.15190312990417, -0.0980434501171241,
        -0.0990297440797205, -0.0989611768448433, 1.15107367264116
    );
    
    color = agx_mat_inv * color;
    
    // Apply saturation adjustment
    float lum = dot(color, vec3(0.2126, 0.7152, 0.0722));
    color = mix(vec3(lum), color, TONEMAP_SATURATION);
    
    // Apply contrast
    color = mix(vec3(0.5), color, TONEMAP_CONTRAST);
    
    // Apply gamma
    color = pow(clamp(color, 0.0, 1.0), vec3(1.0 / TONEMAP_GAMMA));
    
    return mix(vec3(TONEMAP_BLACK_POINT), vec3(1.0), color);
}

#define clamp01(x) clamp(x, 0.0, 1.0)

// thanks to Query's for their AWESOME LUTs

void LookupTable(inout vec3 color) {
    const vec2 inverseSize = vec2(1.0 / 512, 1.0 / 5120);

    const mat2 correctGrid = mat2(
            vec2(1.0, inverseSize.y * 512), vec2(0.0, SELECTED_LUT * inverseSize.y * 512)
    );
    
    vec3 originalColor = color;
    color = clamp01(color);

    float blueColor = color.b * 63.0;

    vec4 quad = vec4(0.0);
    quad.y = floor(floor(blueColor) * 0.125);
    quad.x = floor(blueColor) - (quad.y * 8.0);
    quad.w = floor(ceil(blueColor) * 0.125);
    quad.z = ceil(blueColor) - (quad.w * 8.0);

    vec4 texPos = (quad * 0.125) + (0.123046875 * color.rg).xyxy + 0.0009765625;

    vec3 newColor1, newColor2;
    
    newColor1 = texture2D(colortex7, texPos.xy * correctGrid[0] + correctGrid[1]).rgb;
    newColor2 = texture2D(colortex7, texPos.zw * correctGrid[0] + correctGrid[1]).rgb;

    vec3 lutColor = mix(newColor1, newColor2, fract(blueColor));
    color = mix(originalColor, lutColor, SELECTED_LUT_I);
}

#ifdef BLOOM
    vec2 rescale = max(vec2(viewWidth, viewHeight) / vec2(1920.0, 1080.0), vec2(1.0));
    vec3 GetBloomTile(float lod, vec2 coord, vec2 offset) {
        float scale = exp2(lod);
        vec2 bloomCoord = coord / scale + offset;
        bloomCoord = clamp(bloomCoord, offset, 1.0 / scale + offset);

        vec3 bloom = texture2D(colortex3, bloomCoord / rescale).rgb;
        bloom *= bloom;
        bloom *= bloom;
        
        return bloom * 128.0;
    }

    void DoBloom(inout vec3 color, vec2 coord, float dither, float lViewPos) {
        vec3 blur1 = GetBloomTile(2.0, coord, vec2(0.0      , 0.0   ));
        vec3 blur2 = GetBloomTile(3.0, coord, vec2(0.0      , 0.26  ));
        vec3 blur3 = GetBloomTile(4.0, coord, vec2(0.135    , 0.26  ));
        vec3 blur4 = GetBloomTile(5.0, coord, vec2(0.2075   , 0.26  ));
        vec3 blur5 = GetBloomTile(6.0, coord, vec2(0.135    , 0.3325));
        vec3 blur6 = GetBloomTile(7.0, coord, vec2(0.160625 , 0.3325));
        vec3 blur7 = GetBloomTile(8.0, coord, vec2(0.1784375, 0.3325));

        vec3 blur = (blur1 + blur2 + blur3 + blur4 + blur5 + blur6 + blur7) * 0.14;

        float bloomStrength = BLOOM_STRENGTH + 0.2 * darknessFactor;

        #if defined BLOOM_FOG && defined NETHER && defined BORDER_FOG
            float farM = min(renderDistance, NETHER_VIEW_LIMIT); // consistency9023HFUE85JG
            float netherBloom = lViewPos / clamp(farM, 96.0, 256.0);
            netherBloom *= netherBloom;
            netherBloom *= netherBloom;
            netherBloom = 1.0 - exp(-8.0 * netherBloom);
            netherBloom *= 1.0 - maxBlindnessDarkness;
            bloomStrength = mix(bloomStrength * 0.7, bloomStrength * 1.8, netherBloom);
        #endif

        #ifdef NETHER
        bloomStrength *= 0.1;
        #endif

        #ifdef END
        bloomStrength *= 0.5;
        #endif

        color = mix(color, blur, bloomStrength);
        //color = pow(color, vec3(2.2));
        //color += blur * bloomStrength * (ditherFactor.x + ditherFactor.y);
    }
#endif

//Includes//
#ifdef BLOOM_FOG
    #include "/lib/atmospherics/fog/bloomFog.glsl"
#endif

#ifdef BLOOM
    #include "/lib/util/dither.glsl"
#endif

#if LENSFLARE_MODE > 0 && defined OVERWORLD
    #include "/lib/misc/lensFlare.glsl"
#endif

#include "/lib/antialiasing/autoExposure.glsl"

//Program//
void main() {
    /*#if defined TAA
        vec2 scaledUV = (texCoord) * RENDER_SCALE;
        
        vec3 color = texture2D(colortex0, scaledUV).rgb;
    #else*/
        vec3 color = texelFetch(colortex0, texelCoord, 0).rgb;
    //=#endif

    // Normalized pixel coordinates (from 0 to 1)
    vec2 uv = texCoord * view;

    // Calculate noise and sample texture
    float noise = (fract(sin(dot((texCoord * RENDER_SCALE) * sin(frameTimeCounter) + 1.0, vec2(12.9898,78.233) * 2.0)) * 43758.5453));

    #define FILM_GRAIN_I 2  // [0 1 2 3 4 5 6 7 8 9 10]
    
    color.rgb *= max(noise, 1.0 - (float(FILM_GRAIN_I) / 10));
    color *= 1.3;

    #if defined BLOOM_FOG || LENSFLARE_MODE > 0 && defined OVERWORLD
        float z0 = texture2D(depthtex0, texCoord).r;

        vec4 screenPos = vec4(texCoord, z0, 1.0);
        vec4 viewPos = gbufferProjectionInverse * (screenPos * 2.0 - 1.0);
        viewPos /= viewPos.w;
        float lViewPos = length(viewPos.xyz);

        #if defined DISTANT_HORIZONS && defined NETHER
            float z0DH = texelFetch(dhDepthTex, texelCoord, 0).r;
            vec4 screenPosDH = vec4(texCoord, z0DH, 1.0);
            vec4 viewPosDH = dhProjectionInverse * (screenPosDH * 2.0 - 1.0);
            viewPosDH /= viewPosDH.w;
            lViewPos = min(lViewPos, length(viewPosDH.xyz));
        #endif
    #else
        float lViewPos = 0.0;
    #endif

    vec2 scaledDither = texCoord;
    float dither = texture2D(noisetex, scaledDither * view / 128.0).b;
    #ifdef TAA
        dither = fract(dither + goldenRatio * mod(float(frameCounter), 3600.0));
    #endif

    #ifdef BLOOM_FOG
        color /= GetBloomFog(lViewPos);
    #endif

    #ifdef BLOOM
            DoBloom(color, texCoord, dither, lViewPos);
    #endif

    #ifdef COLORGRADING
        color =
            pow(color.r, GR_RC) * vec3(GR_RR, GR_RG, GR_RB) +
            pow(color.g, GR_GC) * vec3(GR_GR, GR_GG, GR_GB) +
            pow(color.b, GR_BC) * vec3(GR_BR, GR_BG, GR_BB);
        color *= 0.01;
    #endif

    //float filmGrain = dither;
    //color += vec3((filmGrain - 0.25) / 128.0);

    //DoBSLTonemap(color);
    float ignored = dot(color * vec3(0.15, 0.50, 0.35), vec3(0.1, 0.65, 0.6));
    float desaturated = dot(color, vec3(0.15, 0.50, 0.35));
    //color = mix(color, vec3(ignored), exp2((-192) * desaturated));

     // Get auto exposure value (reads from colortex4)
    //float exposure = GetAutoExposure(colortex0, dither);
    
    // Apply exposure
    //#ifdef OVERWORLD
    //    color = ApplyExposure(color, exposure);
    //#endif

    // Apply selected tonemapper
    #if TONEMAP_OPERATOR == 0
        color = Tonemap_Hable(color);
    #elif TONEMAP_OPERATOR == 1
        color = Tonemap_ACES(color);
    #elif TONEMAP_OPERATOR == 2
        color = Tonemap_Lottes(color);
    #elif TONEMAP_OPERATOR == 3
        color = Tonemap_Uchimura(color);
    #elif TONEMAP_OPERATOR == 4
        color = Tonemap_Reinhard(color);
    #elif TONEMAP_OPERATOR == 5
        DoBSLTonemap(color);
    #elif TONEMAP_OPERATOR == 6
        color = Tonemap_AGX(color);
    #else
        color = Tonemap_Hable(color); // Fallback to Hable
    #endif

    #if defined GREEN_SCREEN_LIME || SELECT_OUTLINE == 4
        int materialMaskInt = int(texelFetch(colortex6, texelCoord, 0).g * 255.1);
    #endif

    #ifdef GREEN_SCREEN_LIME
        if (materialMaskInt == 240) { // Green Screen Lime Blocks
            color = vec3(0.0, 1.0, 0.0);
        }
    #endif

    #if SELECT_OUTLINE == 4
        if (materialMaskInt == 252) { // Versatile Selection Outline
            float colorMF = 1.0 - dot(color, vec3(0.25, 0.45, 0.1));
            colorMF = smoothstep1(smoothstep1(smoothstep1(smoothstep1(smoothstep1(colorMF)))));
            color = mix(color, 3.0 * (color + 0.2) * vec3(colorMF * SELECT_OUTLINE_I), 0.3);
        }
    #endif

    #if LENSFLARE_MODE > 0 && defined OVERWORLD
        DoLensFlare(color, viewPos.xyz, dither);
    #endif


    #ifdef OVERWORLD
        LookupTable(color);
    #endif

    /* DRAWBUFFERS:3 */
    gl_FragData[0] = vec4(color, 1.0);

    //if (gl_FragCoord.x < 0.5 && gl_FragCoord.y < 0.5) {
    //    /* DRAWBUFFERS:34 */
    //    gl_FragData[1] = vec4(0.0, exposure, 0.0, 1.0);
    //}
}

#endif

//////////Vertex Shader//////////Vertex Shader//////////Vertex Shader//////////
#ifdef VERTEX_SHADER

noperspective out vec2 texCoord;

#if defined BLOOM_FOG || LENSFLARE_MODE > 0 && defined OVERWORLD
    flat out vec3 upVec, sunVec;
#endif

//Attributes//

//Common Variables//

//Common Functions//

//Includes//

//Program//
void main() {
    gl_Position = ftransform();

    texCoord = (gl_TextureMatrix[0] * gl_MultiTexCoord0).xy;

    #if defined BLOOM_FOG || LENSFLARE_MODE > 0 && defined OVERWORLD
        upVec = normalize(gbufferModelView[1].xyz);
        sunVec = GetSunVector();
    #endif
}

#endif
